# 类型化内存视图

类型化内存视图允许对内存缓冲区有效地访问，比如底层的NumPy数组，而不会引起任何Python开销。内存视图类似于当前的NumPy数组缓冲区支持（`np.ndarray[np.float64_t, ndim=2]`）。但是它们有更多的特性和更干净的语法。

内存视图比旧的NumPy数组缓冲区支持更通用，因为它们可以处理更广泛的数组数据源。例如，它们可以处理C数组和Cython数组类型(Cython数组)。

内存视图可以在任何上下文中使用(函数参数、模块级、cdef类属性等)，并且几乎可以从任何通过[PEP 3118](https://www.python.org/dev/peps/pep-3118/)缓冲区接口公开可写缓冲区的对象中获得。
## 快速开始
如果你习惯于使用NumPy，下面的示例应该可以帮助你开始使用Cython内存视图。
```cython
from cython.view cimport array as cvarray
import numpy as np

# NumPy数组上的内存视图
narr = np.arange(27, dtype=np.dtype("i")).reshape((3, 3, 3))
cdef int [:, :, :] narr_view = narr

# C数组上的内存视图
cdef int carr[3][3][3]
cdef int [:, :, :] carr_view = carr

# Cython数组上的内存视图
cyarr = cvarray(shape=(3, 3, 3), itemsize=sizeof(int), format="i")
cdef int [:, :, :] cyarr_view = cyarr

# 在改变数组之前显示数组的和
print("NumPy sum of the NumPy array before assignments: %s" % narr.sum())

# 我们可以使用单一的陈述方式（使用...或Numpy风格的冒号作为索引）来将一个内存视图
# 中的值复制到另一个
carr_view[...] = narr_view
cyarr_view[:] = narr_view
# 使用NumPy风格的语法将一个值赋给所有元素
narr_view[:, :, :] = 3

# 为了区别数组
carr_view[0, 0, 0] = 100
cyarr_view[0, 0, 0] = 1000

# 对NumPy数组上的内存视图赋值会改变原数组（共用内存）
print("NumPy sum of NumPy array after assignments: %s" % narr.sum())

# 使用内存视图的函数通常不需要GIL
cpdef int sum3d(int[:, :, :] arr) nogil:
    cdef size_t i, j, k, I, J, K
    cdef int total = 0
    I = arr.shape[0]
    J = arr.shape[1]
    K = arr.shape[2]
    for i in range(I):
        for j in range(J):
            for k in range(K):
                total += arr[i, j, k]
    return total

# 一个接受内存视图的函数知道如何使用Numpy数组、C数组以及Cython数组等等
print("Memoryview sum of NumPy array is %s" % sum3d(narr))
print("Memoryview sum of C array is %s" % sum3d(carr))
print("Memoryview sum of Cython array is %s" % sum3d(cyarr))
# ... and of course, a memoryview.
print("Memoryview sum of C memoryview is %s" % sum3d(carr_view))
```
这段代码会给出如下输出：
```cython
NumPy sum of the NumPy array before assignments: 351
NumPy sum of NumPy array after assignments: 81
Memoryview sum of NumPy array is 81
Memoryview sum of C array is 451
Memoryview sum of Cython array is 1351
Memoryview sum of C memoryview is 451
```
## 使用内存视图
### 语法
内存视图使用Python切片语法的方式与NumPy类似。

在一维int缓冲区上创建完整的视图:
```cython
cdef int[:] view1D = exporting_object
```
一个完整的3D视图：
```cython
cdef int[:,:,:] view3D = exporting_object
```
它们也可以很方便地作为函数参数
```cython
def process_3d_buffer(int[:,:,:] view not None):
    ...
```
参数的not None声明自动拒绝将None值作为输入，不这样声明就将允许这样做。None在默认情况下被允许的原因是它被方便地用于返回参数：
```cython
import numpy as np

def process_buffer(int[:,:] input_view not None,
                   int[:,:] output_view=None):

   if output_view is None:
       # Creating a default view, e.g.
       output_view = np.empty_like(input_view)

   # process 'input_view' into 'output_view'
   return output_view
```
Cython将自动拒绝不兼容的缓冲区，例如，将一个三维缓冲区传递给一个需要二维缓冲区的函数将引发一个ValueError错误。
### 索引
在Cython中，内存视图上的索引访问被自动转换为内存地址。下面的代码请求一个C int类型的二维内存视图并对其中元素索引：
```cython
cdef int[:,:] buf = exporting_object

print(buf[1,2])
```
负索引也可以，从相关维度的末尾开始计算：
```cython
print(buf[-1,-2])
```
下面的函数循环遍历二维数组的每个维度，并对每个元素加1：
```cython
import numpy as np

def add_one(int[:,:] buf):
    for x in range(buf.shape[0]):
        for y in range(buf.shape[1]):
            buf[x, y] += 1

# exporting_object必须是使用缓冲区接口的Python对象，比如numpy数组
exporting_object = np.zeros((10, 20), dtype=np.intc)

add_one(exporting_object)
```
索引和切片可以使用GIL，也可以不使用GIL。它的工作原理和NumPy类似。如果每个维度都指定了索引，你将得到基础类型的元素(例如int)。否则，你将得到一个新视图。省略号表示每个未指定维度都有连续的切片：
```cython
import numpy as np

exporting_object = np.arange(0, 15 * 10 * 20, dtype=np.intc).reshape((15, 10, 20))

cdef int[:, :, :] my_view = exporting_object

# 以下等价
my_view[10]
my_view[10, :, :]
my_view[10, ...]
```
### 复制
内存视图可以原地复制:
```cython
import numpy as np

cdef int[:, :, :] to_view, from_view
to_view = np.empty((20, 15, 30), dtype=np.intc)
from_view = np.ones((20, 15, 30), dtype=np.intc)

# 把from_view的元素复制到to_view
to_view[...] = from_view
# 或者
to_view[:] = from_view
# 或者
to_view[:, :, :] = from_view
```
还可以使用copy()和copy_fortran()方法复制它们；参见[C和Fortran连续副本]。
### 转置
在大多数情况下（见下），内存视图可以像NumPy切片一样进行转置：
```cython
import numpy as np

array = np.arange(20, dtype=np.intc).reshape((2, 10))

cdef int[:, ::1] c_contig = array
cdef int[::1, :] f_contig = c_contig.T
```
这给出了数据的一个新的转置过的视图。

转置要求内存视图的所有维度都有一个直接访问内存布局(即没有通过指针的间接方法)。有关详细信息，请参阅[指定更通用的内存布局](#指定更通用的内存布局)。
### 新维度
类似NumPy，可以通过使用```None```索引一个数组来引入新的轴
```cython
cdef double[:] myslice = np.linspace(0, 10, num=50)

# 2D数组，形状为(1, 50)
myslice[None] # or
myslice[None, :]

# 2D数组，形状为(50, 1)
myslice[:, None]

# 3D数组，形状为(1, 10, 1)
myslice[None, 10:-20:2, None]
```
可以将新的axis索引与所有其他形式的索引和切片混合使用。参见另一个[例子](https://docs.scipy.org/doc/numpy/reference/arrays.indexing.html)。
### 只读视图
从Cython 0.28开始，内存视图项类型可以声明为`const`，以支持只读缓冲区作为输入:
```cython
import numpy as np

cdef const double[:] myslice   # const item type => 只读视图

a = np.linspace(0, 10, num=50)
a.setflags(write=False)
myslice = a
```
使用带有二进制Python字符串的非`const`内存视图会产生运行时错误。你可以用`const`内存视图解决这个问题：
```cython
cdef bint is_y_in(const unsigned char[:] string_view):
    cdef int i
    for i in range(string_view.shape[0]):
        if string_view[i] == b'y':
            return True
    return False

print(is_y_in(b'hello world'))   # False
print(is_y_in(b'hello Cython'))  # True
```
注意，这并不要求输入缓冲区是只读的：
```cython
a = np.linspace(0, 10, num=50)
myslice = a   # 可写缓冲区的只读视图
```
可写缓冲区仍然被`const`视图接受，但是只读缓冲区不被非`const`可写视图接受：
```cython
cdef double[:] myslice   # 普通的读/写内存视图

a = np.linspace(0, 10, num=50)
a.setflags(write=False)
myslice = a   # ERROR: requesting writable memory view from read-only buffer!
```
### 与旧式缓冲区支持的比较
你可能更喜欢内存视图而不是旧的语法，因为：

* 语法更简洁
* 内存视图通常不需要GIL（请看[内存视图和GIL](#内存视图和GIL)）
* 内存视图更快

例如，这是与上面`sum3d`函数等价的旧语法：
```cython
cpdef int old_sum3d(object[int, ndim=3, mode='strided'] arr):
    cdef int I, J, K, total = 0
    I = arr.shape[0]
    J = arr.shape[1]
    K = arr.shape[2]
    for i in range(I):
        for j in range(J):
            for k in range(K):
                total += arr[i, j, k]
    return total
```
注意，我们不能像上面`sum3d`的内存视图版本那样对函数的缓冲区版本使用`nogil`，因为缓冲区对象是Python对象。然而，即使我们没有在内存视图中使用`nogil`，它也要快得多。这是导入两个版本后IPython会话的输出：
```cython
In [2]: import numpy as np

In [3]: arr = np.zeros((40, 40, 40), dtype=int)

In [4]: timeit -r15 old_sum3d(arr)
1000 loops, best of 15: 298 us per loop

In [5]: timeit -r15 sum3d(arr)
1000 loops, best of 15: 219 us per loop
```
## Python缓冲区支持
Cython内存视图支持几乎所有导出Python[新风格缓冲区](https://docs.python.org/3/c-api/buffer.html)接口的对象。这是[PEP 3118](https://www.python.org/dev/peps/pep-3118/)中描述的缓冲区接口。NumPy数组和Cython数组都支持这个接口。“几乎全部”是因为Python缓冲区接口允许数据数组中的*元素* 本身是指针；Cython内存视图还不支持这一点。
## 内存布局
缓冲区接口允许对象以多种方式标识底层内存。除了数据元素指针之外，Cython内存视图支持所有Python新型的缓冲区布局。如果内存必须是用于外部例程或代码优化的特定格式，那么了解或指定内存布局是很有用的。
### 背景
其概念如下:有数据访问和数据打包。数据访问意味着直接（无指针）或间接（指针）。数据打包意味着您的数据在内存中可能是连续的，也可能不是连续的，并且可以使用<b><i>跳步（stride）</i></b>来标识每个维度所需的内存连续索引中的跳转。

NumPy数组提供了跳步直接数据访问的良好模型，因此我们将使用它们来复习C和Fortran连续数组的概念以及数据跳步。
### C、Fortran和跳步内存布局的简要回顾
最简单的数据布局可能是C连续数组。这是NumPy和Cython数组中的默认布局。C连续表示数组数据在内存中是连续的（见下文），数组第一个维度中的相邻元素在内存中相距最远，而最后一个维度中的相邻元素距离最近。例如，在NumPy中：
```cython
In [2]: arr = np.array([['0', '1', '2'], ['3', '4', '5']], dtype='S1')
```
这里，arr[0,0]和arr[0,1]在内存中相距一个字节，而arr[0,0]和arr[1,0]相距3个字节。这就引出了<b>*跳步*</b> 的概念。数组的每个轴都有一个步长，即从这个轴上的一个元素到下一个元素所需的字节数。在上面的例子中，坐标轴0和1的步长显然是:
```cython
In [3]: arr.strides
Out[4]: (3, 1)
```
对一个三维C连续数组来说：
```cython
In [5]: c_contig = np.arange(24, dtype=np.int8).reshape((2,3,4))
In [6] c_contig.strides
Out[6]: (12, 4, 1)
```
Fortran连续数组的内存顺序相反，第一个轴上的元素在内存中最接近:
```cython
In [7]: f_contig = np.array(c_contig, order='F')
In [8]: np.all(f_contig == c_contig)
Out[8]: True
In [9]: f_contig.strides
Out[9]: (1, 2, 6)
```
连续数组是指一个连续的内存块包含数组元素的所有数据，因此内存块长度是数组中元素数量和元素大小（以字节为单位）的乘积。在上面的例子中，内存块是2 * 3 * 4 * 1字节长，其中1是int8的长度。

数组可以是连续的，而不需要以C或Fortran顺序：
```cython
In [10]: c_contig.transpose((1, 0, 2)).strides
Out[10]: (4, 12, 1)
```
对一个NumPy数组做切片操作可以很容易地使它不连续：
```cython
In [11]: sliced = c_contig[:,1,:]
In [12]: sliced.strides
Out[12]: (12, 1)
In [13]: sliced.flags
Out[13]:
C_CONTIGUOUS : False
F_CONTIGUOUS : False
OWNDATA : False
WRITEABLE : True
ALIGNED : True
UPDATEIFCOPY : False
```
### 内存视图布局的默认行为
在指定更一般的内存布局时，可以为内存视图的任意维度指定内存布局。若没有指定布局的任意维度，则假定数据访问是直接的，而数据打包是跳步的。例如，这是内存视图的假设：
```cython
int [:, :, :] my_memoryview = obj
```
### C和Fortran的连续内存视图
可以在定义时使用`::1`步语法为内存视图指定C和Fortran连续布局。例如，如果你确定你的内存视图将位于三维C连续布局的顶部，你可以这样写：
```cython
cdef int[:, :, ::1] c_contiguous = c_contig
```
其中`c_contig`可以是一个C连续的NumPy数组。第3个位置的` ::1`表示第3维中的元素在内存中分别相隔1个元素。如果你确定你将有一个三维Fortran连续数组：
```cython
cdef int[::1, :, :] f_contiguous = f_contig
```
如果要传入一个非连续缓冲区，例如：
```cython
# 这个数组是C连续的
c_contig = np.arange(24).reshape((2,3,4))
cdef int[:, :, ::1] c_contiguous = c_contig

# 但这个不是
c_contiguous = np.array(c_contig, order='F')
```
你将会在运行时得到一个`ValueError`：
```cython
/Users/mb312/dev_trees/minimal-cython/mincy.pyx in init mincy (mincy.c:17267)()
    69
    70 # 但这个不是
---> 71 c_contiguous = np.array(c_contig, order='F')
    72
    73 # 在修改数组之前显示所有数组的和

/Users/mb312/dev_trees/minimal-cython/stringsource in View.MemoryView.memoryview_cwrapper (mincy.c:9995)()

/Users/mb312/dev_trees/minimal-cython/stringsource in View.MemoryView.memoryview.__cinit__ (mincy.c:6799)()

ValueError: ndarray is not C-contiguous
```
因此，切片类型规范中的<i>`::1`</i>表示数据在哪个维度上是连续的。它只能用于指定完整的C或Fortran的连续性。
### C和Fortran的连续复制
可以使用.copy()和.copy_fortran()方法创建C或Fortran的连续副本：
```cython
# 这个视图是C连续的
cdef int[:, :, ::1] c_contiguous = myview.copy()

# 这个视图是Fortran连续的
cdef int[::1, :] f_contiguous_slice = myview.copy_fortran()
```
### 指定更通用的内存布局
数据布局可以使用前面看到的`::1`切片语法指定，或者使用`cython.view`中的任何常量指定。如果没有在任何维度中给出指定，则假定数据访问是直接的，且数据打包是跳步的。如果你不知道某个维度是直接的还是间接的（因为你可能从某个库中获得一个带有缓冲区接口的对象），那么你可以指定<b><i>泛型</i></b> 标志，在这种情况下，它将在运行时确定。

标志如下：
* generic - 跳步，直接或间接
* strided - 跳步，直接（这是缺省值）
* indirect - 跳步，间接
* contiguous - 连续，直接
* indirect_contiguous - 指针列表是连续的

它们可以被这样使用：
```cython
from cython cimport view

# 直接访问两个维度，第一个维度上跳步，第二个维度上连续
cdef int[:, ::view.contiguous] a

# 从连续的指针列表到连续的int列表
cdef int[::view.indirect_contiguous, ::1] b

#第一维直接或间接，第二维直接
# 两维都是跳步
cdef int[::view.generic, :] c
```
只有第一维、最后一维或某个间接维度之后的维度可以被指定为连续：
```cython
from cython cimport view

# 有效
cdef int[::view.indirect, ::1, :] a
cdef int[::view.indirect, :, ::1] b
cdef int[::view.indirect_contiguous, ::1, :] c
# 无效
cdef int[::view.contiguous, ::view.indirect, :] d
cdef int[::1, ::view.indirect, :] e
```
<b><i>`contiguous`</i></b>标志和<b><i>`::1`</i></b>指示符的区别在于，前者仅为一个维度指定连续性，而后者为所有后面（Fortran）或前面（C）的维度指定连续性:
```cython
cdef int[:, ::1] c_contig = ...

# 有效
cdef int[:, ::view.contiguous] myslice = c_contig[::2]

# 无效
cdef int[:, ::1] myslice = c_contig[::2]
```
前一种情况有效是因为最后一个维度仍然是连续的，但是第一个维度不再“跟随”最后一个维度（意思是，它已经跳步了，但不再是C或Fortran连续的），因为它是被切片的。
## 内存视图和GIL
正如你在[快速开始](#快速开始)一节中看到的，内存视图通常不需要GIL：
```cython
cpdef int sum3d(int[:, :, :] arr) nogil:
    ...
```
特别的，在内存视图索引、切片或转置时不需要GIL。内存视图在使用复制方法（C和Fortran连续复制）时需要GIL，或者当dtype是对象并且读取或写入对象元素时也需要GIL。
## 内存视图对象和Cython数组
这些类型化内存视图可以转换为Python内存视图对象（*cython.view.memoryview*）。这些Python对象可以像原始内存视图一样索引、切片和转置。它们还可以在任何时候转换回Cython空间内存视图。

它们具有以下特性：
* `shape`：每一维的大小，元组
* `strides`：每一维的跳步大小，字节
* `suboffsets`
* `ndim`：维度数
* `size`：视图中项目总数（`shape`的乘积）
* `itemsize`：视图中项目的大小（以字节为单位）
* `nbytes`：等于`size`乘以`itemsize`
* `base`

当然还有前面提到的T属性（[转置](#转置)）。这些属性具有与NumPy相同的语义。例如，要检索原始对象：
```cython
import numpy
cimport numpy as cnp

cdef cnp.int32_t[:] a = numpy.arange(10, dtype=numpy.int32)
a = a[::2]

print(a)
print(numpy.asarray(a))
print(a.base)

# 这将会打印：
#    <MemoryView of 'ndarray' object>
#    [0 2 4 6 8]
#    [0 1 2 3 4 5 6 7 8 9]
```
注意，这个例子返回了获取视图的原始对象，并且视图也被重新切片了。
## Cython数组
每当复制Cython内存视图（使用*copy* 或*copy_fortran* 方法）时，你将获得新创建的`cython.view.array`对象的一个新的内存视图切片。这个数组也可以手动使用，并将自动分配一个数据块。后面可以将其分配给C或Fortran连续切片（或跳步切片）。它可以这样使用:
```cython
from cython cimport view

my_array = view.array(shape=(10, 2), itemsize=sizeof(int), format="i")
cdef int[:, :] my_slice = my_array
```
它还采用一个可选的参数<i>模式</i>（' c '或' fortran '）和一个布尔型的*allocate_buffer*，它表明当缓冲区超出作用域时是否应该被分配和释放:
```cython
cdef view.array my_array = view.array(..., mode="fortran", allocate_buffer=False)
my_array.data = <char *> my_data_pointer

# 定义一个需要时可以释放数据的函数
my_array.callback_free_data = free
```
你也可以把指针转换成数组，或者把C数组转换成数组：
```cython
cdef view.array my_array = <int[:10, :2]> my_data_pointer
cdef view.array my_array = <int[:, :]> my_c_array
```
当然，你也可以立即把一个cython.view.array分配到一个类型化内存视图切片。一个C数组也可以直接被分配给一个内存视图切片：
```cython
cdef int[:, ::1] myslice = my_2d_c_array
```
数组与内存视图对象一样，可以从Python空间中索引和切片，并且具有与内存视图对象相同的属性。
## CPython的array模块
一种`cython.view.view`的替代是Python标准库中的`array`模块。在Python3中，`array.array`类型本身支持缓冲区接口，因此内存视图无需额外设置就可以在其上工作。

但是，从Cython 0.17开始，也可以在python2中使用这些数组提供缓冲区。这是通过显式cimport `cpython.array`模块实现的，如下所示：
```cython
cimport cpython.array

def sum_array(int[:] view):
    """
    >>> from array import array
    >>> sum_array( array('i', [1,2,3]) )
    6
    """
    cdef int total
    for i in range(view.shape[0]):
        total += view[i]
    return total
```
注意，cimport还支持数组类型的旧缓冲区语法。因此，以下也适用:
```cython
from cpython cimport array

def sum_array(array.array[int] arr):  # 使用旧的缓冲区语法
    ...
```
## 强制转换为NumPy
内存视图（和数组）对象可以强制转为NumPy ndarray，而不必复制数据。你可以这样:
```cython
cimport numpy as np
import numpy as np

numpy_array = np.asarray(<np.int32_t[:10, :10]> my_pointer)
```
当然，这并不是说你必须使用NumPy的类型（比如这里的`np.int32_t`），你可以使用任何可用的类型。
## None Slices
虽然内存视图切片不是对象，但它们可以设置为None，也可以检查它们是否为None:
```cython
def func(double[:] myarray = None):
    print(myarray is None)
```
如果函数需要实际内存视图作为输入，那么最好直接在签名中拒绝None，这个语法在Cython 0.17及以后版本中被支持：
```cython
def func(double[:] myarray not None):
    ...
```
与扩展类的对象属性不同，内存视图切片不能被初始化为None。
## 通过指针从C函数中传入数据
由于C中指针的使用是普遍存在的，这里我们给出一个如何调用参数包含指针的C函数的快速示例。假设你想用NumPy管理一个数组（分配和释放）（也可以是Python数组，或者任何支持缓冲区接口的东西），但是你想用`C_func_file.c`中实现的一个外部C函数对这个数组执行计算:
```C
#include "C_func_file.h"

void multiply_by_10_in_C(double arr[], unsigned int n)
{
    unsigned int i;
    for (i = 0; i < n; i++) {
        arr[i] *= 10;
    }
}
```
这个文件带有一个头文件`C_func_file.h`，包含：
```C
#ifndef C_FUNC_FILE_H
#define C_FUNC_FILE_H

void multiply_by_10_in_C(double arr[], unsigned int n);

#endif
```
其中`arr`指向数组，而`n`是它的大小。

你可以通过以下方式在Cython文件中调用该函数:
 ```cython
cdef extern from "C_func_file.c":
    # 这里包含了C，所以它不需要在外部编译
    pass

cdef extern from "C_func_file.h":
    void multiply_by_10_in_C(double *, unsigned int)

import numpy as np

def multiply_by_10(arr): # 'arr'是一维numpy数组

    if not arr.flags['C_CONTIGUOUS']:
        arr = np.ascontiguousarray(arr) # 创建numpy数组的连续副本

    cdef double[::1] arr_memview = arr

    multiply_by_10_in_C(&arr_memview[0], arr_memview.shape[0])

    return arr


a = np.ones(5, dtype=np.double)
print(multiply_by_10(a))

b = np.ones(10, dtype=np.double)
b = b[::2]  # b不连续

print(multiply_by_10(b))  # 但我们的函数依然和我们的预期运行一致
```
有几点需要注意：
* `::1`请求一个C连续视图，如果缓冲区不是C连续的则会失败。请看[C和Fortran连续内存视图](#C和Fortran的连续内存视图)。
* `&arr_memview[0]`可以理解为“内存视图第一个元素的地址”。对于连续数组，这相当于内存缓冲区的起始地址。
* ：`arr_memview.shape[0]`可以被`arr_memview.size`、`arr.shape[0]`或`arr.size`替换。但`arr_memview.size[0]`效率更高，因为它不需要任何Python交互。
* 如果传入的数组是连续的，`multiply_by_10`将执行就地计算；如果`arr`不连续，则返回一个新的numpy数组。
* 如果使用Python数组而不是numpy数组，则不需要检查数据是否连续存储，因为Python数组总是连续的。请看[使用Python数组]()。

通过这种方式，你可以使用类似调用普通Python函数的方法调用C函数，并将所有内存管理和清理工作留给NumPy数组和Python的对象处理。有关如何编译和调用C文件中的函数的详细信息，请看[使用C库]()。